WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

Program = { SOI ~ Statement+ ~ EOI }

Statement = { Assignment ~ ";" }

Assignment = { "let" ~ Identifier ~ (":" ~ Type)? ~ "=" ~ Expression }

Expression = { 
    FunctionCall |
    IfElseExpression |
    IntegerLiteral | 
    BooleanLiteral | 
    StringLiteral |
    Identifier |
    BinaryExpression |
    UnaryExpression
}

BinaryExpression = { "(" ~ BinaryOperator ~ Expression ~ Expression ~ ")" }
BinaryOperator = @{
    "+" | "-" | "*" | "/" |
    "==" | "!=" |
    // 'or equal to' versions of relation operations must come first
    // before their strict versions to avoid parsing errors because PEGs
    // stop on the first match they found, so always put the longer versions
    // first before the shorter ones.
    "<=" | "<" | ">=" | ">" |
    "||" | "&&"
}

UnaryExpression = { "(" ~ UnaryOperator ~ Expression ~ ")" }
UnaryOperator = { "-" | "!" }

FunctionCall = { "(" ~ Identifier ~ Expression* ~ ")" }

IfElseExpression = { "if" ~ Expression ~ 
    "{" ~ Expression ~ "}" ~ 
    "else" ~ "{" ~ Expression ~ "}"
}

IntegerLiteral = @{ ASCII_DIGIT+ }

BooleanLiteral = @{ "true" | "false" }

StringLiteral = ${ "\"" ~ StringBody ~ "\"" }
StringBody = @{ Char* }
Char = {
    !("\"" | "\\") ~ ANY |
    "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t") |
    "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

Identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

Type = @{ "Number" | "Boolean" | "String" }